cmake_minimum_required(VERSION 3.10)
project(blasfeo_catkin)

find_package(catkin REQUIRED)

include(ExternalProject)

set(BLASFEO_INSTALL_PREFIX ${CATKIN_DEVEL_PREFIX} CACHE STRING "Blasfeo install path")
SET(BLASFEO_INCLUDE_DIR ${BLASFEO_INSTALL_PREFIX}/include)
SET(BLASFEO_LIB_DIR ${BLASFEO_INSTALL_PREFIX}/lib)

file(MAKE_DIRECTORY ${BLASFEO_INCLUDE_DIR})

ExternalProject_Add(blasfeoDownload
  GIT_REPOSITORY https://github.com/giaf/blasfeo
  UPDATE_COMMAND ""
  CMAKE_ARGS
    -DCMAKE_INSTALL_PREFIX=${BLASFEO_INSTALL_PREFIX}
    -DBUILD_SHARED_LIBS:STRING=ON
    -DTARGET:STRING=X64_AUTOMATIC
    -DBLASFEO_EXAMPLES:BOOL=OFF # Don't build examples
  BUILD_COMMAND $(MAKE)
  INSTALL_COMMAND $(MAKE) install
)

# Create a library to be used in the catkin_package LIBRARIES
add_library(${PROJECT_NAME} SHARED IMPORTED)
set_target_properties(${PROJECT_NAME} PROPERTIES IMPORTED_LOCATION ${BLASFEO_LIB_DIR}/libblasfeo.so)
add_dependencies(${PROJECT_NAME} blasfeoDownload)

list(APPEND ${PROJECT_NAME}_EXPORTED_TARGETS blasfeoDownload)

catkin_package(
  INCLUDE_DIRS ${BLASFEO_INCLUDE_DIR}
  LIBRARIES ${PROJECT_NAME}
  CFG_EXTRAS blasfeo-extras.cmake
)

install(DIRECTORY ${BLASFEO_INCLUDE_DIR}/
  DESTINATION ${CATKIN_GLOBAL_INCLUDE_DESTINATION}
)
install(DIRECTORY ${BLASFEO_LIB_DIR}/
  DESTINATION ${CATKIN_GLOBAL_LIB_DESTINATION}
)




